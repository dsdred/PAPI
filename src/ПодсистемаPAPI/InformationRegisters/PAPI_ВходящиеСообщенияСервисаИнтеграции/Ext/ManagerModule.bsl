#Область СлужебныеПроцедурыИФункции

// Функция - Добавить изменить запись
//
// Параметры:
//  СтруктураЗаписи	 - Структура - 
// 
// Возвращаемое значение:
//  Результат - Булево 
//
Функция ДобавитьИзменитьЗапись(СтруктураЗаписи, СтруктураСообщения, Создавать = Истина) Экспорт

	Результат = Ложь;
	
	ВключенПривилегированныйРежим = Ложь;
	Если Не ПривилегированныйРежим() Тогда  
		ВключенПривилегированныйРежим = Истина;
		УстановитьПривилегированныйРежим(ВключенПривилегированныйРежим);
	КонецЕсли;	 
	
	
	Если ТипЗнч(СтруктураЗаписи) <> Тип("Структура") Тогда
		
		ТекстОшибки = НСтр("ru = 'Запись не является Структурой'; en = 'Record is not a Structure'");		
		PAPI_Логирование.ЗаписатьВЛог("PAPI.Ошибка", Перечисления.PAPI_ТипЛога.Ошибка, ТекстОшибки, "РегистрыСведений.PAPI_ВходящиеСообщенияСервисаИнтеграции");
		
		Возврат Результат; 
		
	КонецЕсли;
	
	Если Не СтруктураЗаписи.Свойство("Идентификатор") Тогда 
		
		ТекстОшибки = НСтр("ru = 'Отсутствует свойство ""Идентификатор""'; en = 'Identifier property is missing'");
		PAPI_Логирование.ЗаписатьВЛог("PAPI.Ошибка", Перечисления.PAPI_ТипЛога.Ошибка, ТекстОшибки, "РегистрыСведений.PAPI_ВходящиеСообщенияСервисаИнтеграции");
		
		Возврат Результат;		
		
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(СтруктураЗаписи.Идентификатор) Тогда 
		                                        
		ТекстОшибки = НСтр("ru = 'Не заполнен ""Идентификатор""'; en = 'Identifier not filled in'");
		PAPI_Логирование.ЗаписатьВЛог("PAPI.Ошибка", Перечисления.PAPI_ТипЛога.Ошибка, ТекстОшибки, "РегистрыСведений.PAPI_ВходящиеСообщенияСервисаИнтеграции");
		
		Возврат Результат;		
				
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.PAPI_ВходящиеСообщенияСервисаИнтеграции.СоздатьНаборЗаписей(); 
	НаборЗаписей.Отбор.Идентификатор.Установить(СтруктураЗаписи.Идентификатор);
	
	Если СтруктураЗаписи.Свойство("КодОтправителя") 
		И ЗначениеЗаполнено(СтруктураЗаписи.КодОтправителя)Тогда
		
		НаборЗаписей.Отбор.КодОтправителя.Установить(СтруктураЗаписи.КодОтправителя);
		
	КонецЕсли;
	
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		
		Если Не Создавать Тогда 
			Возврат Истина;	
		КонецЕсли;	
		
		НоваяЗаписьРегистра = НаборЗаписей.Добавить(); 
		
		НоваяЗаписьРегистра.Сообщение = Новый ХранилищеЗначения(СтруктураСообщения);  
		
		МассивПолейРегистра = МассивПолейРегистра();		
	
	Иначе
				
		НоваяЗаписьРегистра = НаборЗаписей[0]; 
		
		МассивПолейРегистра = МассивПолейИзмененияРегистра();	
		
	КонецЕсли;
	
	Попытка
		
		Для Каждого элМассива Из МассивПолейРегистра Цикл 
				
			Если СтруктураЗаписи.Свойство(элМассива) Тогда
				
				НоваяЗаписьРегистра[элМассива] = СтруктураЗаписи[элМассива];
					
			КонецЕсли	
			
		КонецЦикла;
		
		НаборЗаписей.ДополнительныеСвойства.Вставить("PAPIОтключитьПодпискуПередЗаписью");
		
		НаборЗаписей.Записать();
		Результат = Истина;
		
	Исключение

		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());		
		PAPI_Логирование.ЗаписатьВЛог("PAPI.Ошибка", Перечисления.PAPI_ТипЛога.Ошибка, ТекстОшибки, "РегистрыСведений.PAPI_ВходящиеСообщенияСервисаИнтеграции");						
					
	КонецПопытки;
		
	Если ВключенПривилегированныйРежим Тогда 
		ВключенПривилегированныйРежим = Ложь;
		УстановитьПривилегированныйРежим(ВключенПривилегированныйРежим);	
	КонецЕсли;
	
	Возврат Результат; 
	
КонецФункции

// Процедура - Удалить устаревшие записи
//
Процедура УдалитьУстаревшиеЗаписи() Экспорт 
	
	ВключенПривилегированныйРежим = Ложь;
	Если Не ПривилегированныйРежим() Тогда  
		ВключенПривилегированныйРежим = Истина;
		УстановитьПривилегированныйРежим(ВключенПривилегированныйРежим);
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	PAPI_ВходящиеСообщенияСервисаИнтеграции.Идентификатор КАК Идентификатор,
		|	PAPI_ВходящиеСообщенияСервисаИнтеграции.КодОтправителя КАК КодОтправителя
		|ИЗ
		|	РегистрСведений.PAPI_ВходящиеСообщенияСервисаИнтеграции КАК PAPI_ВходящиеСообщенияСервисаИнтеграции
		|ГДЕ
		|	PAPI_ВходящиеСообщенияСервисаИнтеграции.ДатаОтправки <= &ДатаОтправки
		|	И PAPI_ВходящиеСообщенияСервисаИнтеграции.ДанныеПрочитаны";
	
	ДатаОтправки = ТекущаяДатаСеанса();
	Запрос.УстановитьПараметр("ДатаОтправки", ДатаОтправки);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл  
		
		НаборЗаписей = РегистрыСведений.PAPI_ВходящиеСообщенияСервисаИнтеграции.СоздатьНаборЗаписей(); 
		НаборЗаписей.Отбор.Идентификатор.Установить(ВыборкаДетальныеЗаписи.Идентификатор);
		НаборЗаписей.Отбор.КодОтправителя.Установить(ВыборкаДетальныеЗаписи.КодОтправителя);
		НаборЗаписей.Записать();    
		
	КонецЦикла;
	
	Если ВключенПривилегированныйРежим Тогда 
		ВключенПривилегированныйРежим = Ложь;
		УстановитьПривилегированныйРежим(ВключенПривилегированныйРежим);	
	КонецЕсли;

КонецПроцедуры


// Процедура - Запустить повторную загрузку
//
// Параметры:
//  Идентификатор	 - УникальныйИдентификатор - Идентификатор входящего сообщения
//  КодОтправителя	 - Строка - Код отправителя
//
Процедура ЗапуститьПовторнуюЗагрузку(Идентификатор, КодОтправителя) Экспорт 

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	PAPI_ВходящиеСообщенияСервисаИнтеграции.Сообщение КАК Сообщение
		|ИЗ
		|	РегистрСведений.PAPI_ВходящиеСообщенияСервисаИнтеграции КАК PAPI_ВходящиеСообщенияСервисаИнтеграции
		|ГДЕ
		|	PAPI_ВходящиеСообщенияСервисаИнтеграции.Идентификатор = &Идентификатор
		|	И PAPI_ВходящиеСообщенияСервисаИнтеграции.КодОтправителя = &КодОтправителя
		|	И НЕ PAPI_ВходящиеСообщенияСервисаИнтеграции.ДанныеПрочитаны";
	
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	Запрос.УстановитьПараметр("КодОтправителя", КодОтправителя);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		СтруктураВходныхПараметров = ВыборкаДетальныеЗаписи.Сообщение.Получить();
		
		// TODO: Сделать в фоне?
		
		PAPI_РаботаСВходящимиСообщениями.ПодготовитьЧтениеИПрочитатьСообщение(СтруктураВходныхПараметров, Истина); 
		
	КонецЦикла;
		
КонецПроцедуры

// Функция - Массив полей регистра
// 
// Возвращаемое значение:
//  Массив - Поля заполняемые при создании записи
//
Функция МассивПолейРегистра()
	
	МассивПолейРегистра = Новый Массив;  
	
	// Измерения
	МассивПолейРегистра.Добавить("Идентификатор");
	МассивПолейРегистра.Добавить("КодОтправителя");
	
	// Стандартные свойства сообщения
	МассивПолейРегистра.Добавить("ДатаОтправки");
	МассивПолейРегистра.Добавить("ДатаУстаревания");
	//МассивПолейРегистра.Добавить("КодПолучателя");
	МассивПолейРегистра.Добавить("ИдентификаторСообщенияЗапроса");
	
	// Параметры с информацией по обмену
    МассивПолейРегистра.Добавить("ТипСообщения");
	МассивПолейРегистра.Добавить("ТипОбъекта");
	МассивПолейРегистра.Добавить("ИдОбъекта");
	МассивПолейРегистра.Добавить("Представление");
	МассивПолейРегистра.Добавить("ВидИзменения");
	
	// Отметка о прочтении
	МассивПолейРегистра.Добавить("ДанныеПрочитаны");
	МассивПолейРегистра.Добавить("ДатаЧтения"); 
	
	// Ошибка записи
	МассивПолейРегистра.Добавить("ТекстОшибки");
	
	Возврат МассивПолейРегистра;
	
КонецФункции

// Функция - Массив полей изменения регистра
// 
// Возвращаемое значение:
//  Массив - Поля заполняемые при создании записи 
//
Функция МассивПолейИзмененияРегистра()
	
	МассивПолейРегистра = Новый Массив;
	МассивПолейРегистра.Добавить("ДанныеПрочитаны");
	МассивПолейРегистра.Добавить("ДатаЧтения");
	
	МассивПолейРегистра.Добавить("ТекстОшибки");
	
	Возврат МассивПолейРегистра;
	
КонецФункции

#КонецОбласти

