#Область License
 
//MIT License

//Copyright (c) 2025-2026 Dmitrii Sidorenko

//Permission is hereby granted, free of charge, to any person obtaining a copy
//of this software and associated documentation files (the "Software"), to deal
//in the Software without restriction, including without limitation the rights
//to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
//copies of the Software, and to permit persons to whom the Software is
//furnished to do so, subject to the following conditions:

//The above copyright notice and this permission notice shall be included in all
//copies or substantial portions of the Software.

//THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
//IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
//FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
//AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
//LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
//OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
//SOFTWARE.

#КонецОбласти

#Область ОбработчикиСобытийФормы
 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Проверка пригодности платформы
	СтруктураПроверки = ЭтотОбъект().ТекущаяВерсияПлатформы();
	Если СтруктураПроверки.Отработал Тогда 
		ЕстьОшибка 	= Ложь;
		ВерсияПлатформы = СтруктураПроверки.Результат;
	Иначе 
		ЕстьОшибка 	= Истина;
		ТекстОшибки = СтруктураПроверки.ТекстОшибки;  		
	КонецЕсли;

	ПериодОбновленияСписка = 60;
	
	ТипСообщение = Элементы.ТипСообщение.СписокВыбора[0].Значение;
	
КонецПроцедуры     

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВидимостьИДоступностьЭлементов();	
	ОбновитьСписок();  
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСписок

&НаКлиенте
Процедура АвтоОбновлениеСпискаПриИзменении(Элемент)
	
	Если АвтоОбновлениеСписка Тогда 
		ПодключитьОбработчикОжидания("ОбновитьСписок", ПериодОбновленияСписка, Истина);  
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок()  
	
	ОбновитьСписокСоединений();

	// Обновление списка
	Если АвтоОбновлениеСписка Тогда 
		ПодключитьОбработчикОжидания("ОбновитьСписок", ПериодОбновленияСписка, Истина);  
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокСоединений()
	
	Если Список.Количество() > 0 Тогда
		Список.Очистить();
	КонецЕсли;	
	
	МассивСоединений = Новый Массив;
	
	МассивСоединений.Добавить(Новый Структура("ТипСоединения,Соединения",
		"Клиент",WebSocketКлиентСоединения.ПолучитьСоединения()));
	СоединенияСервер = ПолучитьСоединенияНаСервере();
	МассивСоединений.Добавить(Новый Структура("ТипСоединения,Соединения",
		"Сервер",СоединенияСервер));
	
	СоответствиеСоединений = СоответствиеТиповСоединений();
	
	Для Каждого ТекЭлемент Из МассивСоединений Цикл 
		Для Каждого Соединение из ТекЭлемент.Соединения Цикл 
			
			НоваяСтрока = Список.Добавить();
			НоваяСтрока.ТипСоединения 	= ТекЭлемент.ТипСоединения;
			НоваяСтрока.Ключ 			= Соединение.Ключ;
			НоваяСтрока.URLСервера 		= Соединение.URLСервера;
			НоваяСтрока.ПользовательИБ	= Соединение.ИмяПользователяИнформационнойБазы;
			
			Если ТекЭлемент.ТипСоединения = "Клиент" Тогда 
				Состояние = Соединение.ПолучитьСостояние();  
				НоваяСтрока.Состояние = СоответствиеСоединений[Состояние];
			Иначе
				НоваяСтрока.Состояние = Соединение.Состояние;	
			КонецЕсли;	
				
			НоваяСтрока.Модуль 		= Соединение.Обработчики.Модуль;
			НоваяСтрока.Закрытия 	= Соединение.Обработчики.ОбработчикЗакрытияСоединения;
			НоваяСтрока.Открытия 	= Соединение.Обработчики.ОбработчикОткрытияСоединения;
			НоваяСтрока.Ошибки 		= Соединение.Обработчики.ОбработчикОшибки;
			НоваяСтрока.ПолученияСообщения = Соединение.Обработчики.ОбработчикПолученияСообщения;		
			
		КонецЦикла;
	КонецЦикла;	
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСоединенияНаСервере()
	
	СоединенияСервер = WebSocketКлиентСоединения.ПолучитьСоединения();
	ЭлементыСтруктуры = "Ключ,URLСервера,ИмяПользователяИнформационнойБазы";
	ЭлементыОбработчиков = "Модуль,ОбработчикЗакрытияСоединения,ОбработчикОткрытияСоединения,ОбработчикОшибки,ОбработчикПолученияСообщения";	
	
	СоответствиеСоединений = СоответствиеТиповСоединений();
	
	Результат = Новый Массив;
	Для Каждого Соединение Из СоединенияСервер Цикл 
		
		СтруктураСоединения = Новый Структура(ЭлементыСтруктуры, 
			Соединение.Ключ, Соединение.URLСервера, Соединение.ИмяПользователяИнформационнойБазы);
			
		Состояние = Соединение.ПолучитьСостояние();  
		СтруктураСоединения.Вставить("Состояние", СоответствиеСоединений[Состояние]);	
			
		СтруктураОбработчиков = Новый Структура(ЭлементыОбработчиков,	
			Соединение.Обработчики.Модуль,
			Соединение.Обработчики.ОбработчикЗакрытияСоединения,
			Соединение.Обработчики.ОбработчикОткрытияСоединения,
			Соединение.Обработчики.ОбработчикОшибки,
	        Соединение.Обработчики.ОбработчикПолученияСообщения);
			
		СтруктураСоединения.Вставить("Обработчики",СтруктураОбработчиков);	
			
		Результат.Добавить(СтруктураСоединения);     
		
	КонецЦикла;		
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для Каждого ТекущиеДанные Из Список Цикл   
		Если ТекущиеДанные.Выбранное Тогда 
			ТекущиеДанные.Выбранное = Ложь;
		КонецЕсли;	
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для Каждого ТекущиеДанные Из Список Цикл   
		Если Не ТекущиеДанные.Выбранное Тогда 
			ТекущиеДанные.Выбранное = Истина;
		КонецЕсли;	
	КонецЦикла;      
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьСоединения(Команда)
	
	МассивКлючейСервер = Новый Массив;	
	Для Каждого ТекСтрока Из Список Цикл
		Если Не ТекСтрока.Выбранное Тогда 
			Продолжить;
		КонецЕсли;	
			
		Если ТекСтрока.ТипСоединения = "Сервер" Тогда 
			МассивКлючейСервер.Добавить(ТекСтрока.Ключ);
			Продолжить;
		КонецЕсли;		
			
		Соединение = WebSocketКлиентСоединения.ПолучитьСоединение(ТекСтрока.Ключ);  
		Если Соединение <> Неопределено Тогда
			СостояниеСоединения = Соединение.ПолучитьСостояние();
			Если СостояниеСоединения = СостояниеWebSocketСоединения.Открыто Тогда   
				Соединение.Закрыть(); 	
			КонецЕсли;
		КонецЕсли;				
	КонецЦикла;
	ЗакрытьСоединенияНаСервере(МассивКлючейСервер);
	ОбновитьСписок();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗакрытьСоединенияНаСервере(МассивКлючейСервер)  
	
	Для Каждого элМассива Из МассивКлючейСервер Цикл			
		Соединение = WebSocketКлиентСоединения.ПолучитьСоединение(элМассива);  
		Если Соединение <> Неопределено Тогда
			СостояниеСоединения = Соединение.ПолучитьСостояние();
			Если СостояниеСоединения = СостояниеWebSocketСоединения.Открыто Тогда 
				Соединение.Закрыть(); 
			КонецЕсли;
		КонецЕсли;				
	КонецЦикла;     
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСообщение(Команда)
	
 	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	Если ТекущаяСтрока <> Неопределено Тогда 
		Если ТекущаяСтрока.ТипСоединения = "Сервер" Тогда 
			
			ОтправитьТекстНаСервере(ТекущаяСтрока.Ключ, ТекстСообщения, ТипСообщение);
			
		Иначе	

			Соединение = WebSocketКлиентСоединения.ПолучитьСоединение(ТекущаяСтрока.Ключ);  
			Если Соединение <> Неопределено Тогда 
				
				СостояниеСоединения = Соединение.ПолучитьСостояние();
				Если СостояниеСоединения = СостояниеWebSocketСоединения.Открыто Тогда  
					
					Если ТипСообщение = "Текст" Тогда   
						
						ДанныеОтправки = ТекстСообщения;	
						
					Иначе	

						ДанныеОтправки = ПреобразоватьТекстВДвоичныеДанные(ТекстСообщения);
						
					КонецЕсли;	
					
					Соединение.ОтправитьСообщение(ДанныеОтправки);
					
				КонецЕсли; 
				
			КонецЕсли;
					
		КонецЕсли;	
	КонецЕсли;	

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтправитьТекстНаСервере(КлючСоединения, ТекстОтправки, ТипСообщение)
	
	Соединение = WebSocketКлиентСоединения.ПолучитьСоединение(КлючСоединения);   
	Если Соединение <> Неопределено Тогда 
		
		СостояниеСоединения = Соединение.ПолучитьСостояние();
		Если СостояниеСоединения = СостояниеWebSocketСоединения.Открыто Тогда  
			
			Если ТипСообщение = "Текст" Тогда   
				
				ДанныеОтправки = ТекстОтправки;	
				
			Иначе	
				
				ДанныеОтправки = ПреобразоватьТекстВДвоичныеДанные(ТекстОтправки);
				
			КонецЕсли;
			
			Соединение.ОтправитьСообщение(ДанныеОтправки); 
			
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции
   
&НаСервере
Функция ЭтотОбъект(ТекущийОбъект = Неопределено)
	
	Если ТекущийОбъект = Неопределено Тогда
		Возврат РеквизитФормыВЗначение("Объект");
	КонецЕсли; 
	ЗначениеВРеквизитФормы(ТекущийОбъект, "Объект");
	
	Возврат Неопределено;                
	
КонецФункции

&НаКлиенте
Процедура ВидимостьИДоступностьЭлементов()
	
	Элементы.ТекстОшибки.Видимость 	= ЕстьОшибка;    	
	Элементы.СтраницаСоединения.Видимость 	= Не ЕстьОшибка;
	
КонецПроцедуры	

// Преобразовать текст в двоичные данные
//
// Параметры:
//  ТекстДляПреобразования - Строка - текст для преобразования
// 
// Возвращаемое значение:
//  ДвоичныеДанные - Преобразованный текст
//
&НаКлиентеНаСервереБезКонтекста
Функция ПреобразоватьТекстВДвоичныеДанные(знач ТекстДляПреобразования) 
	
	НовыйПоток 	= Новый ПотокВПамяти; 
	ЗаписьТекста= Новый ЗаписьТекста(НовыйПоток);
	ЗаписьТекста.Записать(ТекстДляПреобразования);  
	ЗаписьТекста.Закрыть();
	Результат = НовыйПоток.ЗакрытьИПолучитьДвоичныеДанные();
	
	Возврат Результат;
	
КонецФункции	

&НаКлиентеНаСервереБезКонтекста
Функция СоответствиеТиповСоединений() 
	
	СоответствиеСоединений = Новый Соответствие;
	СоответствиеСоединений.Вставить(СостояниеWebSocketСоединения.Открывается,"Открывается");
	СоответствиеСоединений.Вставить(СостояниеWebSocketСоединения.Открыто, 	 "Открыто");
	СоответствиеСоединений.Вставить(СостояниеWebSocketСоединения.Закрывается,"Закрывается");
	СоответствиеСоединений.Вставить(СостояниеWebSocketСоединения.Закрыто, 	 "Закрыто");
	Возврат СоответствиеСоединений;
	
КонецФункции

#КонецОбласти


