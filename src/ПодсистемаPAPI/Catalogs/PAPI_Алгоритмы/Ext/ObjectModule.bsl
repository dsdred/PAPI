#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	 
	Если Не ЭтоГруппа Тогда  
		Если Не ПроверитьЗаполнение() Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ТекстОшибки = ПроверитьУникальностьЭлементов();
	
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		
		Отказ = Истина;  
		PAPI_ОбщегоНазначенияКлиентСервер.КраткоеСообщениеПользователю(ТекстОшибки, "Объект.ИмяАлгоритма");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Контролирует уникальность элемента в базе.
//
//  Возвращаемое значение:
//      Текст ошибки.
//
Функция ПроверитьУникальностьЭлементов()
	
	Результат = "";	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.PAPI_Алгоритмы
		|ГДЕ 
		|	НЕ ПометкаУдаления
		|	И ИмяАлгоритма = &ИмяАлгоритма
		|	И Ссылка <> &Ссылка
		|");
	Запрос.УстановитьПараметр("Ссылка",       Ссылка);
	Запрос.УстановитьПараметр("ИмяАлгоритма", ИмяАлгоритма);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда 
		
		ТекстОшибки =	НСтр("ru = 'Алгоритм с именем %1 уже существует';
						|en = 'Algorithm Name %1 already exists'");
		Результат 	=	СтрШаблон(ТекстОшибки, Строка(ИмяАлгоритма));
	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'; en = 'Invalid object call on the client.'");
#КонецЕсли