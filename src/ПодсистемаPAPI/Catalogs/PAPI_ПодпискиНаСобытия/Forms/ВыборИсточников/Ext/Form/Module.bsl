#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("МассивВыбранных") Тогда 
		МассивВыбранных = Параметры.МассивВыбранных;
	Иначе	
		МассивВыбранных = Новый Массив;
	КонецЕсли;

	Если Параметры.Свойство("ТипПодписки") Тогда
		ТипПодписки = PAPI_ПодпискиНаСобытия.ПолучитьТипПодпискиСтрокой(Параметры.ТипПодписки);
	КонецЕсли;	
	
	// Доступные Источники
	ОбъектовМетаданныхПоПодписке = PAPI_ПодпискиНаСобытия.ПолучитьИсточникиПоТипу(ТипПодписки);
		
	// Получаем все источники подписок
	СписокОбъектовМетаданных = PAPI_ПодпискиНаСобытия.ПолучитьВсеИсточники("");
		
	// Объекты метаданных
	перСписокМетаданных = НовоеДеревоМетаданных();
	Для Каждого элМассива Из СписокОбъектовМетаданных Цикл 
		
		ИмяОбъектаМетаданных = PAPI_ПодпискиНаСобытия.ИмяОбъектаМетаданных(элМассива);	
		Если ПустаяСтрока(ИмяОбъектаМетаданных) Тогда 
			Продолжить; 	
		КонецЕсли;
		
		ВерхнийУровеньДерева = перСписокМетаданных.Строки.Добавить();
		ВерхнийУровеньДерева.Имя 	 	= элМассива;
	    ВерхнийУровеньДерева.Синоним 	= элМассива;
		ВерхнийУровеньДерева.ПолноеИмя	= элМассива;   
		
		Если МассивВыбранных.Найти(элМассива) <> Неопределено Тогда 
			ВерхнийУровеньДерева.Пометка = 1;
		Иначе
			ВерхнийУровеньДерева.Пометка = 0; 
		КонецЕсли;	
		
		Если ОбъектовМетаданныхПоПодписке.Найти(элМассива) <> Неопределено Тогда 
			ВерхнийУровеньДерева.Подписка = 1;
		Иначе
			ВерхнийУровеньДерева.Подписка = 0; 
		КонецЕсли;	
		
		ВсегоПодчиненные = 0;
		ВсегоПомеченныеПодчиненных = 0;
		Если ИмяОбъектаМетаданных = "Перерасчеты" Тогда 
			
			Для Каждого ОбъектМетаданных Из Метаданные.РегистрыРасчета Цикл  
				
				Для Каждого текПерерасчет Из ОбъектМетаданных.Перерасчеты Цикл  
					
					ВсегоПодчиненные = ВсегоПодчиненные + 1;
					
					ПодчиненныйУровеньДерева = ВерхнийУровеньДерева.Строки.Добавить(); 
					ПодчиненныйУровеньДерева.Имя		 = текПерерасчет.Имя;
					ПодчиненныйУровеньДерева.Синоним     = ?(НЕ ПустаяСтрока(текПерерасчет.Синоним), текПерерасчет.Синоним, текПерерасчет.Имя);
					ПодчиненныйУровеньДерева.ПолноеИмя 	 = ВерхнийУровеньДерева.Имя + "." + текПерерасчет.Имя;
					
					Если МассивВыбранных.Найти(ПодчиненныйУровеньДерева.ПолноеИмя) <> Неопределено Тогда 
						ПодчиненныйУровеньДерева.Пометка = 1;	
					Иначе
						ПодчиненныйУровеньДерева.Пометка = ВерхнийУровеньДерева.Пометка;
					КонецЕсли; 	
					
					Если ПодчиненныйУровеньДерева.Пометка = 1 Тогда 
						ВсегоПомеченныеПодчиненных = ВсегоПомеченныеПодчиненных + 1;
					КонецЕсли;
					
					ПодчиненныйУровеньДерева.Подписка = ВерхнийУровеньДерева.Подписка; 
					
				КонецЦикла;	
				
			КонецЦикла;	
						
		Иначе
			
			Для Каждого ОбъектМетаданных Из Метаданные[ИмяОбъектаМетаданных] Цикл
				
				ВсегоПодчиненные = ВсегоПодчиненные + 1;
				
				ПодчиненныйУровеньДерева 			= ВерхнийУровеньДерева.Строки.Добавить(); 
				ПодчиненныйУровеньДерева.Имя		= ОбъектМетаданных.Имя;
				ПодчиненныйУровеньДерева.Синоним    = ?(НЕ ПустаяСтрока(ОбъектМетаданных.Синоним), ОбъектМетаданных.Синоним, ОбъектМетаданных.Имя);
				ПодчиненныйУровеньДерева.ПолноеИмя 	= ВерхнийУровеньДерева.Имя + "." + ОбъектМетаданных.Имя;
				
				Если МассивВыбранных.Найти(ПодчиненныйУровеньДерева.ПолноеИмя) <> Неопределено Тогда 
					ПодчиненныйУровеньДерева.Пометка = 1;	
				Иначе
					ПодчиненныйУровеньДерева.Пометка = ВерхнийУровеньДерева.Пометка;
				КонецЕсли; 
				
				Если ПодчиненныйУровеньДерева.Пометка = 1 Тогда 
					ВсегоПомеченныеПодчиненных = ВсегоПомеченныеПодчиненных + 1;
				КонецЕсли;
				
				ПодчиненныйУровеньДерева.Подписка = ВерхнийУровеньДерева.Подписка
				
			КонецЦикла;
			
		КонецЕсли;
			
		Если ВсегоПодчиненные > 0
			И ВсегоПомеченныеПодчиненных > 0
			И ВсегоПодчиненные <> ВсегоПомеченныеПодчиненных Тогда 
			ВерхнийУровеньДерева.Пометка = 2;		
		КонецЕсли;  
		
	КонецЦикла;    
	
	ЗначениеВРеквизитФормы(перСписокМетаданных, "ДеревоМетаданных"); 
	
КонецПроцедуры  

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоМетаданных

&НаКлиенте
Процедура ДеревоМетаданныхПометкаПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.ДеревоМетаданных.ТекущаяСтрока;
	Если ТекущаяСтрока <> Неопределено Тогда 
		ИзменениеПометки(ТекущаяСтрока);
	КонецЕсли;	
КонецПроцедуры 

&НаКлиенте
Процедура Сохранить(Команда)
	
	СписокОбъектовИИ = ДеревоМетаданных.ПолучитьЭлементы();
	
	МассивИсточников = Новый Массив;
	Для Каждого ВерхнийУровеньДерева Из СписокОбъектовИИ Цикл 
		
		Если ВерхнийУровеньДерева.Пометка = 1 Тогда
			
			// Берем только главный источник
			
			МассивИсточников.Добавить(Новый Структура("Источник, Подписка", 
									ВерхнийУровеньДерева.ПолноеИмя,
									?(ВерхнийУровеньДерева.Подписка = 1, Истина, Ложь))); 
			
		ИначеЕсли ВерхнийУровеньДерева.Пометка = 0	Тогда 
			
			// нет нужных Источников
			
	    Иначе
			
			// Забираем подчиненные Источники
			
			НижнийУровеньДерева = ВерхнийУровеньДерева.ПолучитьЭлементы();	

			Для Каждого ТекущиеДанные Из НижнийУровеньДерева Цикл 
				
				Если ТекущиеДанные.Пометка = 1 Тогда   
					
					МассивИсточников.Добавить(Новый Структура("Источник, Подписка", 
										ТекущиеДанные.ПолноеИмя,ТекущиеДанные.Подписка));
											
				КонецЕсли;
										
			КонецЦикла;
			
		КонецЕсли;	
			
	КонецЦикла;
	
    ОповеститьОВыборе(МассивИсточников);

КонецПроцедуры

&НаКлиенте
Процедура Отменить(Команда)
	ЭтаФорма.Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИзменениеПометки(ТекущаяСтрока) 
	
	ЭлементДанных = ДеревоМетаданных.НайтиПоИдентификатору(ТекущаяСтрока);
	PAPI_ОбщегоНазначенияКлиентСервер.ИзменениеПометки(ЭлементДанных);
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция НовоеДеревоМетаданных()
	
    // "Объекты метаданных"
	ДеревоМетаданных = Новый ДеревоЗначений;
	ДеревоМетаданных.Колонки.Добавить("Имя", 			Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки()));
	ДеревоМетаданных.Колонки.Добавить("Синоним", 		Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки()));
    ДеревоМетаданных.Колонки.Добавить("ПолноеИмя", 		Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки()));

	ДеревоМетаданных.Колонки.Добавить("Пометка", 		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1)));
	ДеревоМетаданных.Колонки.Добавить("ИндексКартинки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(2)));	
	ДеревоМетаданных.Колонки.Добавить("Подписка", 		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1)));

	Возврат ДеревоМетаданных;
		
КонецФункции

#КонецОбласти

